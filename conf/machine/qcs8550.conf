#@TYPE: Machine
#@NAME: qcs8550
#@DESCRIPTION: Machine configuration for the development board, with Qualcomm QCS8550

require conf/machine/include/qcom-qcs8550.inc

MACHINE_FEATURES = "usbhost usbgadget alsa wifi"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = " \
                      qcom/qcs8550-addons-aim300-aiot.dtb \
                      qcom/qcs8550-rb5gen2-hdk.dtb \
                      qcom/sm8550-qrd.dtb \
                      qcom/sm8550-mtp.dtb \
                      "

# Additional dtbo to overlay on top of kernel devicetree files
KERNEL_TECH_DTBOS[qcs8550-addons-aim300-aiot] = "qcs8550-cnss.dtbo qcs8550-display.dtbo qcs8550-graphics.dtbo qcs8550-qps615.dtbo"
KERNEL_TECH_DTBOS[sm8550-qrd] = ""
KERNEL_TECH_DTBOS[sm8550-mtp] = ""

EFI_ARCH = "aa64"
UKI_FILENAME = "uki.efi"
UKI_DTB = "0"

# Additional Kernel cmdline parameters for debug builds
DBG_CMDLINE = "${@oe.utils.conditional('DEBUG_BUILD','1','slub_debug=FZP,zs_handle,zspage; FZPU','',d)}"

# Additional Kernel cmdline parameters for performance optimizations
CONSOLE_CMDLINE = "${@oe.utils.conditional('PERFORMANCE_BUILD','1','console=','console=ttyMSM0,115200n8 earlycon',d)}"

KERNEL_CMDLINE_EXTRA ?= "root=/dev/disk/by-partlabel/system rw rootwait ${CONSOLE_CMDLINE} pcie_pme=nomsi net.ifnames=0 ${DBG_CMDLINE}"

# Disable serial console for performance
SERIAL_CONSOLES = "${@oe.utils.conditional('PERFORMANCE_BUILD','1','','115200;ttyMSM0',d)}"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS += " \
    kernel-modules \
"

MACHINE_EXTRA_RDEPENDS += " \
    packagegroup-firmware-qcs8550 \
    wlan-devicetree \
    displaydevicetree \
    graphicsdevicetree \
    qps615-devicetree \
"

KERNEL_MODULE_AUTOLOAD += "realtek"

QCOM_BOOTIMG_ROOTFS ?= "/dev/disk/by-partlabel/system"

OVERLAYFS_ETC_MOUNT_POINT = "/mnt/overlay"
OVERLAYFS_ETC_DEVICE = "/dev/disk/by-partlabel/overlay"
OVERLAYFS_ETC_FSTYPE ?= "ext4"
OVERLAYFS_ETC_USE_ORIG_INIT_NAME = "0"
